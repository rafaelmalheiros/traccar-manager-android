apply plugin: 'com.android.application'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId 'org.traccar.manager'
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 20
        versionName '2.9'
    }

    flavorDimensions 'default'
    productFlavors {
        regular {
            isDefault.set(true)
        }
        google
        geoplace {
            applicationId 'br.com.geoplace'
            buildConfigField("String", "SERVICE_URL", "\"https://sistema.geoplace.com.br\"")
            versionCode 4
            versionName '1.3'
        }
        wig {
            applicationId 'br.com.muitoalemderastreamento'
            buildConfigField("String", "SERVICE_URL", "\"https://sistema.muitoalemderastreamento.com.br\"")
            versionCode 4
            versionName '1.3'
        }

        wgt {
            applicationId 'br.com.wgtrastreadores'
            buildConfigField("String", "SERVICE_URL", "\"https://sistema.wgtrastreadores.com.br\"")
            versionCode 4
            versionName '1.3'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    buildToolsVersion = '29.0.3'
}

dependencies {
    implementation 'com.google.android.material:material:1.0.0'
    googleImplementation 'com.google.firebase:firebase-core:17.2.2'
    googleImplementation 'com.google.firebase:firebase-messaging:20.1.0'
    googleImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains('Google')) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'io.fabric'

    task copyJson(type: Copy) {
        from '../../traccar-shared/google-services'
        into '.'
        include 'traccar-manager-android.json'
        rename('traccar-manager-android.json', 'google-services.json')
    }

    afterEvaluate {
        preBuild.dependsOn copyJson
    }
}
